plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.dynamicwebservice'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.a09090443:base-spring-boot-starter:3.2.5.4'
    implementation 'io.github.a09090443:web-spring-boot-starter:3.2.5.1'
    implementation 'io.github.a09090443:db-spring-boot-starter:3.2.5.1'
    implementation 'io.github.a09090443:web-service-spring-boot-starter:3.2.5.2'

    implementation 'org.springframework.boot:spring-boot-devtools:3.2.5'
    implementation 'net.sf.jasperreports:jasperreports:6.20.6'
    implementation 'com.github.librepdf:openpdf:1.3.30'
    implementation 'org.eclipse.persistence:eclipselink:3.0.4'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'com.h2database:h2:2.2.220'
    implementation 'org.flywaydb:flyway-core:10.14.0'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('buildAngular', Exec) {
    workingDir file('../mockwebservice-frontend').absolutePath
    environment 'PATH', file('../tools/node-v20.15.0-win-x64').absolutePath + File.pathSeparator + System.env.PATH
    def angularEnv = project.hasProperty('env') && project.property('env') == 'pro' ? 'production' : 'development'
    commandLine 'npm.cmd', 'run', 'build', '--', '--configuration', angularEnv, '--base-href', '/mockwebservice/'
}

tasks.register('copyAngularBuild', Copy) {
    dependsOn 'buildAngular'
    from '../mockwebservice-frontend/dist'
    into 'src/main/resources/static'
}

tasks.named('processResources') {
    dependsOn 'copyAngularBuild'
}

def env = project.hasProperty('env') ? project.property('env') : 'dev'

tasks.register('forceUpdateProperties') {
    doLast {
        println "Forcing update of properties files for environment: ${env}"

        def resourcesDir = layout.buildDirectory.dir("resources/main").get().asFile
        resourcesDir.mkdirs() // 確保目錄存在

        // 強制更新 resources.properties
        def resourcesFile = new File(resourcesDir, "resources.properties")
        resourcesFile.delete()
        file("src/main/resources/resources_${env}.properties").withInputStream { input ->
            resourcesFile.withOutputStream { output ->
                output << input
            }
        }

        // 強制更新 data-source.properties
        def dataSourceFile = new File(resourcesDir, "data-source.properties")
        dataSourceFile.delete()
        file("src/main/resources/data-source_${env}.properties").withInputStream { input ->
            dataSourceFile.withOutputStream { output ->
                output << input
            }
        }

        // 刪除其他環境特定文件
        resourcesDir.listFiles().each { file ->
            if (file.name.startsWith("resources_") || file.name.startsWith("data-source_")) {
                file.delete()
            }
        }

        println "Properties files have been forcibly updated."
    }
}

tasks.named('bootJar') {
    dependsOn 'forceUpdateProperties'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(["-parameters", "-Xlint:deprecation"])
    options.annotationProcessorPath = configurations.annotationProcessor
}

tasks.named('test') {
    useJUnitPlatform()
}
